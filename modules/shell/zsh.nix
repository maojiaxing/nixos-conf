{ lib, config, options, pkgs, ...}:

with lib;
let
  cfg = config.modules.shell.zsh;
in {

  options.modules.shell.zsh = with types; {
    enable = mkBoolOpt true;

    rcInit = mkOpt' lines "" ''
      Zsh lines to be written to $XDG_CONFIG_HOME/zsh/extra.zshrc and sourced by
      $XDG_CONFIG_HOME/zsh/.zshrc
    '';

    envInit = mkOpt' lines "" ''
      Zsh lines to be written to ${config.home.configDir}/zsh/extra.zshenv and
      sourced by $XDG_CONFIG_HOME/zsh/.zshenv
    '';

    rcFiles  = mkOpt (listOf (either str path)) [];
    envFiles = mkOpt (listOf (either str path)) [];
  };

  config = mkIf cfg.enable {
     programs.zsh = {
      enable = true;
      enableCompletion = true;
      enableGlobalCompInit = false;
      histFile = "$XDG_STATE_HOME/zsh/history";
      enableLsColors = false;
    };

    user.packages = with pkgs; [
      bat      # a better cat
      bc
      dust     # a better du
      eza      # a better ls
      fasd
      fd
      fzf
      gnumake
    ];

    environment.variables = {
      ZDOTDIR = "$XDG_CONFIG_HOME/zsh";
      ZGEN_DIR = "$XDG_DATA_HOME/zgenom";
      _FASD_DATA = "$XDG_CACHE_HOME/fasd";
      _FASD_VIMINFO = "$XDG_CACHE_HOME/viminfo";
    };

    systemd.user.tmpfiles.rules = [
      "d %h/.cache/zsh 750 - - - -"
      "d %h/.local/state/zsh 700 - - - -"
    ];

    home.configFile = {
      "zsh" = {
        source = "/home/${config.user.name}/zsh"; recursive = true;
      };

      zsh/extra.zshrc".text = ''
         # This file was autogenerated, do not edit it!

         # modules.shell.zsh.rcInit
         hey.cache fasd --init posix-alias zsh-{hook,{c,w}comp{,-install}}
         hey.cache ${pkgs.coreutils}/bin/dircolors -b
         ${cfg.rcInit}

         # modules.shell.zsh.rcFiles
         ${concatMapStringsSep "\n\n" builtins.readFile cfg.rcFiles}
      '';

      "zsh/.zshenv".text = ''
        # This file is autogenerated, do not edit it!

        # Be more restrictive with permissions in $HOME; no one has any business
        # reading things that don't belong to them.
        if (( EUID != 0 )); then
          umask 027
        else
          # Be even less permissive if root.
          umask 077
        fi

        # modules.shell.zsh.envInit
        ${cfg.envInit}

        # modules.shell.zsh.envFiles
        ${concatMapStringsSep "\n\n" builtins.readFile cfg.envFiles}
      '';
    };
  };
}
